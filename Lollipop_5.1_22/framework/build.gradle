import java.nio.file.*

apply plugin: 'maven'
apply plugin: 'signing'

group 'com.rover12421.android.hide'
version 'Lollipop_5.1_22'

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign project.configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: rootProject.sonatypeUsername, password: rootProject.sonatypePassword)
        }

        configuration = configurations.archives

        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        pom.artifactId = project.name
        pom.version = project.version
        pom.groupId = project.group

        pom.project {
            name project.name
            description "Android Hidden API / ${project.name}"
            url 'http://shaka.rover.plus'
            packaging 'jar'
            licenses {
                license {
                    name 'MIT License'
                    url 'https://opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/rover12421/AndroidHideApi/tree/master'
                connection 'scm:git:git://github.com/rover12421/AndroidHideApi.git'
                developerConnection 'scm:git:git@github.com:rover12421/AndroidHideApi.git'
            }
            developers {
                developer {
                    id 'rover12421'
                    name 'Rover12421'
                    email 'rover12421@163.com'
                }
            }
        }
    }
}

task replace_jar {
    doLast {
        def from = "${project.projectDir}/libs/classes.jar"
        def des = "${project.buildDir}/libs/${project.name}-${project.version}.jar"
        println "replace_jar : ${from}"
        Files.copy(Paths.get(from), Paths.get(des), StandardCopyOption.REPLACE_EXISTING)
    }
}

uploadArchives.dependsOn replace_jar
